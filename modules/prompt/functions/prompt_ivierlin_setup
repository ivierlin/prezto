function +vi-git_status {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]='%F{red}!%f'
  fi
}

function prompt_ivierlin_precmd {
  vcs_info
  symbol="%B%(?.%F{green}.%F{red})❱%f%b"
  histnum="%(?.%F{8}.%F{red})#%h%f"
  local showhost=${SSH_TTY:+":%F{yellow}%m%f "}
  login="!.%F{red}%n%f${showhost:- }.${SSH_TTY:+%n}"


  pwd="${PWD/#$HOME/~}"
  local base="${pwd:0:1}${${pwd:1}%%/*}"
  local pat="/~"
  local dir="/$pwd:t"
  dir="${dir/#$pat/~}"
  if [[ $dir == $base ]]; then
	base=
  fi
  local mid="${${pwd#$base}%$dir}"

  pwd="$base%F{blue}%B%12</…<%b%f$mid%<<%B$dir%b"
}

function prompt_ivierlin_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add a hook for calling info functions before each command.
  add-zsh-hook precmd prompt_ivierlin_precmd

  # Set git-info parameters.
  zstyle ':vcs_info:*' enable git
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr '%F{green}▲%f'
  zstyle ':vcs_info:*' unstagedstr '*'
  zstyle ':vcs_info:*' formats ' %F{8}branch:%F{yellow}%b%f%c%u'
  zstyle ':vcs_info:*' actionformats " %b%c%u|%F{cyan}%a%f"
  zstyle ':vcs_info:*' branchformat '%b|%F{cyan}%r%f'
  zstyle ':vcs_info:git*+set-message:*' hooks git_status

  # Define prompts.
    
  PROMPT='%(${login}${showhost})${pwd}${symbol} '
  RPROMPT='${vcs_info_msg_0_} ${histnum}'

}

function prompt_ivierlin_help {
  cat <<EOH
This prompt's last command exit status symbol is customizable:

  prompt ivierlin [<symbol>]

If this option is not provided, the symbol defaults to ☻.
EOH
}

prompt_ivierlin_setup "$@"
